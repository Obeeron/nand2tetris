/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Register(in=regIn, load=true, out=out, out=regOut);
    Inc16(in=regOut, out=regInc);
    Mux16(a=regOut, b=regInc, sel=inc, out=regIncOrSame);
    Mux16(a=regIncOrSame, b=in, sel=load, out=regIncOrSameOrLoad);
    Mux16(a=regIncOrSameOrLoad, b=false, sel=reset, out=regIn);
}
